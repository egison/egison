;;
;; Base.egi
;;

(define $buildin-data-matcher
  (matcher
    {[,$val []
      (lambda [$tgt] (if (eq? val tgt) {[]} {}))]
     [$ [something]
      (lambda [$tgt] {tgt})]
     }))

(define $id (lambda [$x] x))

(define $bool buildin-data-matcher)

(define $or
  (lambda [$b1 $b2]
    (if b1
        #t
        b2)))

(define $and
  (lambda [$b1 $b2]
    (if b1
        b2
        #f)))

(define $not
  (lambda [$b]
    (match b bool
      {[,#t #f]
       [,#f #t]})))

(define $char buildin-data-matcher)

(define $eq?/m
  (lambda [$a $x $y]
    (match x a
      {[,y #t]
       [_ #f]})))

(define $compose
  (lambda [$f $g]
    (lambda $x
      (apply g (apply f x)))))

(define $compose3
  (lambda [$f $g $h]
    (lambda $x
      (apply h (apply g (apply f x))))))