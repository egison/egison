-- Riemann curvature tensor of S7

def x := [| α, β, γ, δ, ε, ζ, η |]

def X := [| r * cos α
          , r * sin α * cos β
          , r * sin α * sin β * cos γ
          , r * sin α * sin β * sin γ * cos δ
          , r * sin α * sin β * sin γ * sin δ * cos ε
          , r * sin α * sin β * sin γ * sin δ * sin ε * cos ζ
          , r * sin α * sin β * sin γ * sin δ * sin ε * sin ζ * cos η
          , r * sin α * sin β * sin γ * sin δ * sin ε * sin ζ * sin η
          |]

-- Local basis
def e := (flip ∂/∂) x~# X_#

-- Metric tensor
def g_i_j := generateTensor (\[a, b] -> V.* e_a e_b) [7, 7]
def g~i~j := M.inverse g_#_#

-- Christoffel symbols
def Γ_i_j_k := (1 / 2) * (∂/∂ g_i_k x~j + ∂/∂ g_i_j x~k - ∂/∂ g_j_k x~i)

def Γ~i_j_k := withSymbols [m]
  g~i~m . Γ_m_j_k

-- Riemann curvature
def R~i_j_k_l := withSymbols [m]
  ∂/∂ Γ~i_j_l x~k - ∂/∂ Γ~i_j_k x~l + Γ~m_j_l . Γ~i_m_k - Γ~m_j_k . Γ~i_m_l

-- Ricci curvature
def Ric_i_j := withSymbols [m]
  sum (contract R~m_i_m_j)

Ric_#_#

-- Scalar curvature
def scalarCurvature := withSymbols [i, j]
  g~i~j . Ric_i_j

-- Expected: 42 / r^2 (for S^7, scalar curvature is n(n-1)/r^2 = 7*6/r^2 = 42/r^2)
scalarCurvature
