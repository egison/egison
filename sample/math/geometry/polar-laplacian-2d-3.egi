-- Polar Laplacian in 2D using coordinate-free formula

def x : Vector MathExpr := [| r, θ |]

def X : Vector MathExpr := [| r * cos θ, r * sin θ |]

-- Local basis
def e : Matrix MathExpr := (flip ∂/∂) x~# X_#

-- Metric tensor
def g_i_j : Matrix MathExpr := generateTensor (\[a, b] -> V.* e_a e_b) [2, 2]
def g~i~j : Matrix MathExpr := withSymbols [i, j] (unitTensor [2, 2])_i_j / g_i_j

assertEqual "Metric tensor"
  g_#_#
  [| [| 1, 0 |], [| 0, r^2 |] |]_#_#

def sqrtG : MathExpr := sqrt (M.det g_#_#)

assertEqual "sqrt(det(g))"
  sqrtG
  r

-- Laplacian using coordinate-free formula: 1/sqrt(g) * d/dx^i (sqrt(g) * g^ij * d/dx^j f)
def Laplacian : MathExpr := withSymbols [i, j]
  sum (contract (∂/∂ (sqrtG * (g~i~j . ∂/∂ (f r θ) x~j)) x~i)) / sqrtG

assertEqual "Laplacian in polar coordinates (coordinate-free)"
  Laplacian
  (f|1|1 r θ + f|1 r θ / r + f|2|2 r θ / r^2)
