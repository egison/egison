-- Hodge Laplacian in spherical coordinates

def N := 3

def x := [| r, θ, φ |]

def g_i_j := [| [| 1, 0, 0 |], [| 0, r^2, 0 |], [| 0, 0, r^2 * (sin θ)^2 |] |]

def g~i~j := M.inverse g_#_#

-- Exterior derivative
def d X := (flip ∂/∂) x~# !. X

-- Hodge star operator
def hodge A :=
  let k := dfOrder A
   in withSymbols [i, j]
        sqrt (abs (M.det g_#_#)) *
        foldl
          (.)
          ((ε' N k)_(i_1)..._(i_N) . A..._(j_1)..._(j_k))
          (map (\n -> g~(i_n)~(j_n)) (between 1 k))

-- Codifferential
def δ A :=
  let r := dfOrder A
   in ((-1)^(N * r + 1)) * hodge (d (hodge A))

-- Laplacian
def Δ A :=
  match dfOrder A as integer with
    | #0 -> δ (d A)
    | #N -> d (δ A)
    | _ -> d (δ A) + δ (d A)

-- Apply to scalar function
Δ (f r θ φ)
