-- Parameters
def x := [| θ, φ, ψ |]

def X := [| r * cos θ
          , r * sin θ * cos φ
          , r * sin θ * sin φ * cos ψ
          , r * sin θ * sin φ * sin ψ
          |]

-- Local basis
def e := (flip ∂/∂) x~# X_#

-- Metric tensors
def g_i_j := generateTensor (\[a, b] -> V.* e_a e_b) [3, 3]
def g~i~j := M.inverse g_#_#

assertEqual "Metric tensor g_#_#"
  g_#_#
  [| [| r^2, 0, 0 |], [| 0, r^2 * (sin θ)^2, 0 |], [| 0, 0, r^2 * (sin θ)^2 * (sin φ)^2 |] |]_#_#

-- Christoffel symbols
def Γ_i_j_k := (1 / 2) * (∂/∂ g_i_k x~j + ∂/∂ g_i_j x~k - ∂/∂ g_j_k x~i)

def Γ~i_j_k := withSymbols [m]
  g~i~m . Γ_m_j_k

-- Riemann curvature
def R~i_j_k_l := withSymbols [m]
  ∂/∂ Γ~i_j_l x~k - ∂/∂ Γ~i_j_k x~l + Γ~m_j_l . Γ~i_m_k - Γ~m_j_k . Γ~i_m_l

assertEqual "Riemann curvature R~#_#_1_2"
  R~#_#_1_2
  [| [| 0, (sin θ)^2, 0 |], [| -1, 0, 0 |], [| 0, 0, 0 |] |]~#_#

-- Ricci curvature
def Ric_i_j := withSymbols [m]
  sum (contract R~m_i_m_j)

assertEqual "Ricci curvature Ric_#_#"
  Ric_#_#
  [| [| 2, 0, 0 |], [| 0, 2 * (sin θ)^2, 0 |], [| 0, 0, 2 * (sin θ)^2 * (sin φ)^2 |] |]_#_#

-- Scalar curvature
def scalarCurvature := withSymbols [i, j]
  g~i~j . Ric_i_j

assertEqual "scalar curvature"
  scalarCurvature
  (6 / r^2)
